apply plugin: 'com.android.application'

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {

        applicationId "edu.cnm.deepdive.quotesbackground"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "String", "BASE_URL", "\"http://api.forismatic.com/api/1.0/\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {

    // .jar-based libraries included in project
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Basic Android widgets
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Material Design components
    implementation 'com.google.android.material:material:1.1.0'

    // Preference/settings dependencies
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.navigation:navigation-fragment:2.3.0'
    implementation 'androidx.navigation:navigation-ui:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Lifecycle (LiveData and ViewModel)
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // ReactiveX (reactive streams for asynchronous processing)
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Room (ORM for SQLite) with ReactiveX integration
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    // WorkManager (deferred background tasks) with ReactiveX integration
    def work_version = "2.4.0"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-rxjava2:$work_version"

    // Gson (Google JSON parser)
    implementation 'com.google.code.gson:gson:2.8.6'

    // Retrofit (REST client) with ReactiveX integration
    def retrofit_version = "2.6.2"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // OkHttp logging interceptor (log Retrofit requests & responses)
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.2'

    // Stetho (allows Chromium-based inspection)
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // Unit and UI test dependencies
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}